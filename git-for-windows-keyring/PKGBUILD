# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

pkgname=git-for-windows-keyring
_ver_base=1.1
pkgver=1.1.1.4474718
pkgrel=1
pkgdesc='Git for Windows PGP keyring'
arch=('i686' 'x86_64')
groups=('base')
license=('GPL')
install="${pkgname}.install"
source=("git-for-windows.gpg"
	"git-for-windows-revoked"
	"git-for-windows-trusted")
sha256sums=('0efe9648f5830f8ea3d11c4e93b5cc332011e01b16e70bab17ff8fea394ee7b1'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'f58a54ff351ef51bc7fbf70a6f1c1e29060501a5a08f723dc113e34b98c13b83')

pkgver() {
  cd "$startdir"
  test ! -f "$(git rev-parse --git-path shallow)" || git -c http.sslbackend fetch --unshallow
  tip_commit="$(git rev-list -1 HEAD -- . ':(exclude)./PKGBUILD' ':(exclude)./.gitignore')"
  printf "%s.%s.%s" "${_ver_base}" \
    "$(git rev-list --count $tip_commit -- . ':(exclude)./PKGBUILD' ':(exclude)./.gitignore')" \
    "$(git rev-parse --short $tip_commit)"
}

package() {
  cd "${srcdir}"
  install -d -m755 $pkgdir/usr/share/pacman/keyrings
  install -m644 git-for-windows.gpg $pkgdir/usr/share/pacman/keyrings
  install -m644 git-for-windows-revoked $pkgdir/usr/share/pacman/keyrings
  install -m644 git-for-windows-trusted $pkgdir/usr/share/pacman/keyrings
}
