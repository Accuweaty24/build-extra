# Maintainer: Johannes Schindelin/Matthew J Cheetham

_realname="git-credential-manager-core"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.435.9025
pkgrel=1
_realver=$pkgver
_realtag=v${pkgver%.*}-beta
pkgdesc="Credential Manager for Git"
install=git-credential-manager-core.install
arch=('any')
project_url="https://github.com/microsoft/Git-Credential-Manager-Core"
zip_url="${project_url}/releases/download/${_realtag}/gcmcore-win-x86-${_realver}.zip"
src_zip_url="${project_url}/archive/${_realtag}.zip"
license=('MIT')
groups=('VCS')

source=("${zip_url}" "$src_zip_url")

sha256sums=('f3910896b1b401743c63d1d0b64a804ea29c1bb42326c38902d5075451b45f98'
            '6d4aab3b156c54138d301dce1ad3191a1a30e4e0391c3d9053285b8373a9c826')

package() {
  prefix="$pkgdir/${MINGW_PREFIX}"
  srcdir2="${srcdir}/"
  install -d -m755 "${prefix}"/libexec/git-core
  install -m755 "$srcdir2"/*.{dll,exe,config} "${prefix}"/libexec/git-core
  install -d -m755 "${prefix}"/doc/git-credential-manager-core
  install -m644 "$srcdir2"/Git-Credential-Manager-Core-${_realtag#v}/{README.md,LICENSE,NOTICE} "${prefix}"/doc/git-credential-manager-core
}
