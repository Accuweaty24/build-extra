# Maintainer: Johannes Schindelin/Matthew J Cheetham

_realname="git-credential-manager-core"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.246.34937
pkgrel=1
_realver=$pkgver
_realtag=v${pkgver%.*}-beta
pkgdesc="Credential Manager for Git"
arch=('any')
project_url="https://github.com/microsoft/Git-Credential-Manager-Core"
zip_url="${project_url}/releases/download/${_realtag}/gcmcore-win-x86-${_realver}.zip"
src_zip_url="${project_url}/archive/${_realtag}.zip"
license=('MIT')
groups=('VCS')

source=("${zip_url}" "$src_zip_url")

sha256sums=('7a26982e030b2a7a10695f21dd7b39d1d58ecf01f8fd1fed9974912701f7ff8e'
            '76fd4ed1ad0a53f6b058243d5ba0e3fc38c31626cc9d2627c612530b5e542ecb')

package() {
  prefix="$pkgdir/${MINGW_PREFIX}"
  srcdir2="${srcdir}/"
  install -d -m755 "${prefix}"/libexec/git-core
  install -m755 "$srcdir2"/*.{dll,exe,config} "${prefix}"/libexec/git-core
  install -d -m755 "${prefix}"/doc/git-credential-manager-core
  install -m644 "$srcdir2"/Git-Credential-Manager-Core-${_realtag#v}/{README.md,LICENSE,NOTICE} "${prefix}"/doc/git-credential-manager-core
}
