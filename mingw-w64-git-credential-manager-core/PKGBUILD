# Maintainer: Johannes Schindelin/Matthew J Cheetham

_realname="git-credential-manager-core"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.605.12951
pkgrel=1
_realver=$pkgver
_realtag=v${pkgver%.*}
pkgdesc="Credential Manager for Git"
install=git-credential-manager-core.install
arch=('any')
project_url="https://github.com/GitCredentialManager/git-credential-manager"
zip_url="${project_url}/releases/download/${_realtag}/gcmcore-win-x86-${_realver}.zip"
src_zip_url="${project_url}/archive/${_realtag}.zip"
license=('MIT')
groups=('VCS')
options=('!strip')

source=("${zip_url}" "$src_zip_url")

sha256sums=('3433ebedd65fd7b19edbb52410a89621a4e32f00933b8b00f730cf540af04898'
            'efa417d55d48c24f80de0a3358e9bb29e0b454a36168a76ae0b4f9688f05101b')

package() {
  prefix="$pkgdir/${MINGW_PREFIX}"
  srcdir2="${srcdir}/"
  install -d -m755 "${prefix}"/libexec/git-core
  install -m755 "$srcdir2"/*.{dll,exe,config} "${prefix}"/libexec/git-core
  install -d -m755 "${prefix}"/doc/git-credential-manager-core
  install -m644 "$srcdir2"/git-credential-manager-${_realtag#v}/{README.md,LICENSE,NOTICE} "${prefix}"/doc/git-credential-manager-core
}
